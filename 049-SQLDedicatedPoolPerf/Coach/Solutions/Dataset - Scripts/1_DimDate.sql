
RENAME OBJECT [DIMDATE] TO [DIMDATE_OLD]


CREATE TABLE [DBO].[DIMDATE]
(
	[DATEKEY] [INT] NOT NULL,
	[FULLDATEALTERNATEKEY] [DATE] NOT NULL,
	[DAYNUMBEROFWEEK] [TINYINT] NOT NULL,
	[ENGLISHDAYNAMEOFWEEK] [NVARCHAR](10) NOT NULL,
	[SPANISHDAYNAMEOFWEEK] [NVARCHAR](10) NOT NULL,
	[FRENCHDAYNAMEOFWEEK] [NVARCHAR](10) NOT NULL,
	[DAYNUMBEROFMONTH] [TINYINT] NOT NULL,
	[DAYNUMBEROFYEAR] [SMALLINT] NOT NULL,
	[WEEKNUMBEROFYEAR] [TINYINT] NOT NULL,
	[ENGLISHMONTHNAME] [NVARCHAR](10) NOT NULL,
	[SPANISHMONTHNAME] [NVARCHAR](10) NOT NULL,
	[FRENCHMONTHNAME] [NVARCHAR](10) NOT NULL,
	[MONTHNUMBEROFYEAR] [TINYINT] NOT NULL,
	[CALENDARQUARTER] [TINYINT] NOT NULL,
	[CALENDARYEAR] [SMALLINT] NOT NULL,
	[CALENDARSEMESTER] [TINYINT] NOT NULL,
	[FISCALQUARTER] [TINYINT] NOT NULL,
	[FISCALYEAR] [SMALLINT] NOT NULL,
	[FISCALSEMESTER] [TINYINT] NOT NULL
)
WITH
(
	DISTRIBUTION = REPLICATE,
	CLUSTERED COLUMNSTORE INDEX
)
GO

;WITH 
	CTE AS (SELECT 1 DAYSNUM UNION ALL SELECT 1)
	,CTE4 AS (SELECT A.DAYSNUM FROM CTE A, CTE B)
	,CTE16 AS (SELECT A.DAYSNUM FROM CTE4 A, CTE4 B)
	,CTE256 AS (SELECT A.DAYSNUM FROM CTE16 A, CTE16 B)
	,CTE65536 AS (SELECT A.DAYSNUM FROM CTE256 A, CTE256 B)
	,RESULTS AS (SELECT ROW_NUMBER() OVER(ORDER BY DAYSNUM)-1 DAYSNUM FROM CTE65536)
	,DATES AS (	SELECT CAST(DATEADD(D,DAYSNUM,'2001-07-01') AS DATE) [DATE] FROM RESULTS WHERE DATEADD(D,DAYSNUM,'2001-07-01')<=GETDATE())
	,STEP1 AS ( SELECT DISTINCT CONVERT(VARCHAR(8), DATE,112) DATEKEY
		, DATE FULLDATEALTERNATEKEY
		, DATEPART(WEEKDAY,DATE) [DAYNUMBEROFWEEK] 
		, DATENAME(WEEKDAY, DATE) [ENGLISHDAYNAMEOFWEEK]
		, DATEPART(YEAR,DATE) [CALENDARYEAR] 
		, DATEPART(MONTH,DATE) [MONTHNUMBEROFYEAR] 
		, DATENAME(MONTH, DATE) [ENGLISHMONTHNAME]
		, DATEPART(DAY,DATE) [DAYNUMBEROFMONTH] 
		, DATEPART(DAYOFYEAR,DATE) [DAYNUMBEROFYEAR] 
		, DATEPART(WEEK,DATE) [WEEKNUMBEROFYEAR] 
		, DATEPART(QUARTER,DATE) [CALENDARQUARTER] 
		FROM DATES )
	INSERT INTO DIMDATE
	SELECT 
		STEP1.[DATEKEY]
		,STEP1.[FULLDATEALTERNATEKEY]
		,STEP1.[DAYNUMBEROFWEEK]
		,D.[ENGLISHDAYNAMEOFWEEK]
		,D.[SPANISHDAYNAMEOFWEEK]
		,D.[FRENCHDAYNAMEOFWEEK]
		,STEP1.[DAYNUMBEROFMONTH]
		,STEP1.[DAYNUMBEROFYEAR]
		,STEP1.[WEEKNUMBEROFYEAR]
		,STEP1.[ENGLISHMONTHNAME]
		,M.[SPANISHMONTHNAME]
		,M.[FRENCHMONTHNAME]
		,STEP1.[MONTHNUMBEROFYEAR]
		,STEP1.[CALENDARQUARTER]
		,STEP1.[CALENDARYEAR]
		,CASE WHEN STEP1.[CALENDARQUARTER] >= 3 THEN 2 ELSE 1 END AS [CALENDARSEMESTER]
		,CASE WHEN STEP1.[CALENDARQUARTER] <= 2 THEN 2 + STEP1.[CALENDARQUARTER] ELSE STEP1.[CALENDARQUARTER]-2 END AS  FISCALQUARTER
		,CASE WHEN STEP1.[CALENDARQUARTER] >= 3 THEN STEP1.[CALENDARYEAR]+1 ELSE STEP1.[CALENDARYEAR] END AS  [FISCALYEAR]
		,CASE WHEN STEP1.[CALENDARQUARTER] <= 2 THEN 2 ELSE 1 END AS [FISCALSEMESTER]
	FROM STEP1
		INNER JOIN (SELECT DISTINCT [ENGLISHDAYNAMEOFWEEK],[SPANISHDAYNAMEOFWEEK],[FRENCHDAYNAMEOFWEEK] FROM [DBO].[DIMDATE_OLD]) D
			ON D.[ENGLISHDAYNAMEOFWEEK] = STEP1.[ENGLISHDAYNAMEOFWEEK]
		INNER JOIN (SELECT DISTINCT [ENGLISHMONTHNAME],[SPANISHMONTHNAME],[FRENCHMONTHNAME] FROM [DBO].[DIMDATE_OLD]) M
			ON M.[ENGLISHMONTHNAME] = STEP1.[ENGLISHMONTHNAME]			
